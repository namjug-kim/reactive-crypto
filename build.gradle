apply plugin: 'java'

group 'com.njkimg.reactive-crypto'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile 'com.github.jitpack:gradle-simple:1.0'
}

allprojects {
    apply plugin: 'jacoco'
}

subprojects {
    group 'com.njkimg.reactive-crypto'

    ext {
        reactorVersion = '3.2.8.RELEASE'
        kotlinJvmVersion = '1.3.21'
        log4j2Version = '2.11.2'
        slf4jVersion = '1.7.26'
    }

    buildscript {
        repositories {
            mavenCentral()
        }
        dependencies {
            // kotlin plugin
            classpath group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: '1.3.31'
        }
    }

    apply plugin: 'java'

    repositories {
        mavenCentral()
        jcenter()
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.pinterest:ktlint:0.32.0"
        // common
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

        // reactor
        implementation group: 'io.projectreactor', name: 'reactor-core', version: '${reactorVersion}'
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.3.31'

        // logging
        implementation group: 'io.github.microutils', name: 'kotlin-logging', version: '1.6.24'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: "${slf4jVersion}"

        // test
        testCompile group: 'junit', name: 'junit', version: '4.11'
        testCompile group: 'org.assertj', name: 'assertj-core', version: '3.11.1'
        testCompile group: 'io.projectreactor', name: 'reactor-test', version: reactorVersion

        testRuntimeOnly group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${log4j2Version}"
        testRuntimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${log4j2Version}"
        testRuntimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${log4j2Version}"
    }

    task ktlint(type: JavaExec, group: "verification") {
        description = "Check Kotlin code style."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "src/**/*.kt"
        // to generate report in checkstyle format prepend following args:
        // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
        // see https://github.com/pinterest/ktlint#usage for more
    }
    check.dependsOn ktlint

    task ktlintFormat(type: JavaExec, group: "formatting") {
        description = "Fix Kotlin code style deviations."
        main = "com.pinterest.ktlint.Main"
        classpath = configurations.ktlint
        args "-F", "src/**/*.kt"
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}